import qs
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import QtQuick
import Quickshell.Io
import Quickshell
import Quickshell.Wayland
import Quickshell.Hyprland

Scope { // Scope
    id: root
    property bool detach: false
    property bool pin: false
    readonly property bool sidebarOpen: GlobalStates.sidebarLeftOpen
    property Component contentComponent: SidebarLeftContent {}
    property Item sidebarContent
    readonly property bool centerOnly: Config.options.bar.layouts.leftLayout.length === 0 && Config.options.bar.layouts.rightLayout.length === 0 && !Config.options.bar.vertical

    function toggleDetach() {
        root.detach = !root.detach;
    }

    Process { // Dodge cursor away, pin, move cursor back
        id: pinWithFunnyHyprlandWorkaroundProc
        property var hook: null
        property int cursorX;
        property int cursorY;
        function doIt() {
            command = ["hyprctl", "cursorpos"]
            hook = (output) => {
                cursorX = parseInt(output.split(",")[0]);
                cursorY = parseInt(output.split(",")[1]);
                doIt2();
            }
            running = true;
        }
        function doIt2(output) {
            command = ["bash", "-c", "hyprctl dispatch 'hl.dsp.cursor.move({x=9999,y=9999})'"];
            hook = () => {
                doIt3();
            }
            running = true;
        }
        function doIt3(output) {
            root.pin = !root.pin;
            command = ["bash", "-c", `sleep 0.01; hyprctl dispatch 'hl.dsp.cursor.move({x=${cursorX},y=${cursorY}})'`];
            hook = null
            running = true;
        }
        stdout: StdioCollector {
            onStreamFinished: {
                pinWithFunnyHyprlandWorkaroundProc.hook(text);
            }
        }
    }

    function togglePin() {
        if (!root.pin) pinWithFunnyHyprlandWorkaroundProc.doIt()
        else root.pin = !root.pin;
    }

    function ensureSidebarContent() {
        if (root.sidebarContent) return
        root.sidebarContent = contentComponent.createObject(null, {
            "scopeRoot": root,
        })
        const host = root.detach ? detachedSidebarLoader.item : sidebarLoader.item
        if (host) host.contentParent.children = [root.sidebarContent]
    }

    function releaseSidebarContent() {
        if (!root.sidebarContent || root.detach || root.pin
                || GlobalStates.sidebarLeftOpen) return
        const oldContent = root.sidebarContent
        root.sidebarContent = null
        oldContent.parent = null
        oldContent.destroy()
    }

    // Keep the lightweight shell hot for repeated toggles. Heavy pages gate
    // themselves on visibility and the whole tree is released after 30 s.
    Timer {
        id: contentUnloadTimer
        interval: 30000
        repeat: false
        onTriggered: root.releaseSidebarContent()
    }

    Connections {
        target: GlobalStates
        function onSidebarLeftOpenChanged() {
            if (GlobalStates.sidebarLeftOpen) {
                contentUnloadTimer.stop()
                root.ensureSidebarContent()
            } else if (!root.detach && !root.pin) {
                contentUnloadTimer.restart()
            }
        }
    }

    Component.onCompleted: {
        if (GlobalStates.sidebarLeftOpen)
            root.ensureSidebarContent()
    }

    onDetachChanged: {
        root.ensureSidebarContent()
        const dockedWindow = sidebarLoader.item;
        if (dockedWindow)
            GlobalFocusGrab.removeDismissable(dockedWindow);
        if (root.detach) {
            sidebarContent.parent = null; // Detach content from sidebar
            sidebarLoader.active = false; // Unload sidebar
            detachedSidebarLoader.active = true; // Load detached window
            detachedSidebarLoader.item.contentParent.children = [sidebarContent];
        } else {
            sidebarContent.parent = null; // Detach content from window
            detachedSidebarLoader.active = false; // Unload detached window
            sidebarLoader.active = true; // Load sidebar
            sidebarLoader.item.contentParent.children = [sidebarContent];
        }
    }

    onPinChanged: {
        if (root.pin) contentUnloadTimer.stop()
        else if (!GlobalStates.sidebarLeftOpen && !root.detach) contentUnloadTimer.restart()
    }

    Loader {
        id: sidebarLoader
        active: true
        
        sourceComponent: PanelWindow { // Window
            id: panelWindow

            readonly property bool animatedEntrance: WM.compositor !== "hyprland"

            property bool reallyVisible: false
            visible: reallyVisible

            function syncFocusGrab() {
                GlobalFocusGrab.removeDismissable(panelWindow);
                if (panelWindow.visible
                        && GlobalStates.sidebarLeftOpen
                        && !root.detach)
                    GlobalFocusGrab.addDismissable(panelWindow);
            }

            Component.onCompleted: {
                reallyVisible = GlobalStates.sidebarLeftOpen;
                panelWindow.syncFocusGrab();
            }

            Connections {
                target: GlobalStates
                function onSidebarLeftOpenChanged() {
                    panelWindow.syncFocusGrab();
                    if (GlobalStates.sidebarLeftOpen) {
                        closeAnimTimer.stop();
                        panelWindow.reallyVisible = true;
                    } else if (panelWindow.animatedEntrance) {
                        closeAnimTimer.restart();
                    } else {
                        panelWindow.reallyVisible = false;
                    }
                }
            }

            Timer {
                id: closeAnimTimer
                interval: Appearance.animation.elementMoveExit.duration
                onTriggered: panelWindow.reallyVisible = false
            }

            property bool extend: false
            property real sidebarWidth: panelWindow.extend ? Appearance.sizes.sidebarWidthExtended : Appearance.sizes.sidebarWidth
            property var contentParent: sidebarLeftBackground

            function hide() {
                GlobalStates.sidebarLeftOpen = false
            }

            exclusionMode: ExclusionMode.Normal
            exclusiveZone: root.pin ? sidebarWidth : 0
            implicitWidth: Appearance.sizes.sidebarWidthExtended + Appearance.sizes.elevationMargin
            WlrLayershell.namespace: "quickshell:sidebarLeft"
            // Hyprland 0.49: OnDemand is Exclusive, Exclusive just breaks click-outside-to-close
            WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand
            color: "transparent"

            anchors {
                top: true
                left: true
                bottom: true
            }

            margins {
                top: {
                    if (Config.options.bar.bottom) return 0;
                    if (Config?.options.bar.autoHide.enable) return 0;
                    if (!centerOnly) return 0;
                    switch (Config.options.bar.cornerStyle) {
                    case 0: return -Appearance.sizes.barHeight;
                    case 1: return -Appearance.sizes.barHeight + Appearance.sizes.hyprlandGapsOut;
                    case 2: return -Appearance.sizes.barHeight + Appearance.sizes.hyprlandGapsOut;
                    case 3: return -Appearance.sizes.barHeight - Appearance.sizes.hyprlandGapsOut;
                    default: return 0;
                    }
                }
                bottom: {
                    if (!Config.options.bar.bottom) return 0;
                    if (Config?.options.bar.autoHide.enable) return 0;
                    if (!centerOnly) return 0;
                    switch (Config.options.bar.cornerStyle) {
                    case 0: return -Appearance.sizes.barHeight;
                    case 1: return -Appearance.sizes.barHeight + Appearance.sizes.hyprlandGapsOut;
                    case 2: return -Appearance.sizes.barHeight + Appearance.sizes.hyprlandGapsOut;
                    case 3: return -Appearance.sizes.barHeight - Appearance.sizes.hyprlandGapsOut;
                    default: return 0;
                    }
                }
            }

            mask: Region {
                item: panelWindow.animatedEntrance ? fullMaskArea : sidebarLeftBackground
            }

            onVisibleChanged: panelWindow.syncFocusGrab()
            onScreenChanged: panelWindow.syncFocusGrab()
            Component.onDestruction: {
                GlobalFocusGrab.removeDismissable(panelWindow);
            }
            Connections {
                target: GlobalFocusGrab
                function onDismissed() {
                    panelWindow.hide();
                }
            }

            // Content
            Item {
                id: fullMaskArea
                anchors.fill: parent
            }

            MouseArea {
                id: outsideClickArea
                anchors.fill: parent
                enabled: panelWindow.animatedEntrance
                visible: panelWindow.animatedEntrance
                onClicked: panelWindow.hide()
            }

            StyledRectangularShadow {
                target: sidebarLeftBackground
                radius: sidebarLeftBackground.radius
            }
            Rectangle {
                id: sidebarLeftBackground
                anchors.top: parent.top
                anchors.topMargin: Appearance.sizes.hyprlandGapsOut
                width: panelWindow.sidebarWidth - Appearance.sizes.hyprlandGapsOut - Appearance.sizes.elevationMargin
                height: parent.height - Appearance.sizes.hyprlandGapsOut * 2
                color: Appearance.colors.colLayer0
                border.width: 1
                border.color: Appearance.colors.colLayer0Border
                radius: Appearance.rounding.screenRounding - Appearance.sizes.hyprlandGapsOut + 1

                readonly property bool animatedEntrance: panelWindow.animatedEntrance
                readonly property bool sidebarOpen: GlobalStates.sidebarLeftOpen
                x: Appearance.sizes.hyprlandGapsOut - (animatedEntrance && !sidebarOpen ? width : 0)

                Behavior on x {
                    enabled: sidebarLeftBackground.animatedEntrance
                    NumberAnimation {
                        duration: sidebarLeftBackground.sidebarOpen
                            ? Appearance.animation.elementMoveEnter.duration
                            : Appearance.animation.elementMoveExit.duration
                        easing.type: sidebarLeftBackground.sidebarOpen
                            ? Appearance.animation.elementMoveEnter.type
                            : Appearance.animation.elementMoveExit.type
                        easing.bezierCurve: sidebarLeftBackground.sidebarOpen
                            ? Appearance.animation.elementMoveEnter.bezierCurve
                            : Appearance.animation.elementMoveExit.bezierCurve
                    }
                }

                Behavior on width {
                    animation: Appearance.animation.elementMove.numberAnimation.createObject(this)
                }

                MouseArea {
                    anchors.fill: parent
                    onClicked: (mouse) => { mouse.accepted = true }
                }

                Keys.onPressed: (event) => {
                    if (event.key === Qt.Key_Escape) {
                        panelWindow.hide();
                    }
                    if (event.modifiers === Qt.ControlModifier) {
                        if (event.key === Qt.Key_O) {
                            panelWindow.extend = !panelWindow.extend;
                        } else if (event.key === Qt.Key_D) {
                            root.toggleDetach();
                        } else if (event.key === Qt.Key_P) {
                            root.togglePin();
                        }
                        event.accepted = true;
                    }
                }
            }
        }
    }

    Loader {
        id: detachedSidebarLoader
        active: false

        sourceComponent: FloatingWindow {
            id: detachedSidebarRoot
            property var contentParent: detachedSidebarBackground
            color: "transparent"

            visible: GlobalStates.sidebarLeftOpen
            onVisibleChanged: {
                if (!visible) GlobalStates.sidebarLeftOpen = false;
            }
            
            Rectangle {
                id: detachedSidebarBackground
                anchors.fill: parent
                color: Appearance.colors.colLayer0

                Keys.onPressed: (event) => {
                    if (event.modifiers === Qt.ControlModifier) {
                        if (event.key === Qt.Key_D) {
                            root.toggleDetach();
                        }
                        event.accepted = true;
                    }
                }
            }
        }
    }

    IpcHandler {
        target: "sidebarLeft"

        function toggle(): void {
            GlobalStates.sidebarLeftOpen = !GlobalStates.sidebarLeftOpen
        }

        function close(): void {
            GlobalStates.sidebarLeftOpen = false
        }

        function open(): void {
            GlobalStates.sidebarLeftOpen = true
        }
    }

    CompositorGlobalShortcut {
        name: "sidebarLeftToggle"
        description: "Toggles left sidebar on press"

        onPressed: {
            GlobalStates.sidebarLeftOpen = !GlobalStates.sidebarLeftOpen;
        }
    }

    CompositorGlobalShortcut {
        name: "sidebarLeftOpen"
        description: "Opens left sidebar on press"

        onPressed: {
            GlobalStates.sidebarLeftOpen = true;
        }
    }

    CompositorGlobalShortcut {
        name: "sidebarLeftClose"
        description: "Closes left sidebar on press"

        onPressed: {
            GlobalStates.sidebarLeftOpen = false;
        }
    }

    CompositorGlobalShortcut {
        name: "sidebarLeftToggleDetach"
        description: "Detach left sidebar into a window/Attach it back"

        onPressed: {
            root.detach = !root.detach;
        }
    }

}
